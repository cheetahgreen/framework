version 1;
version glsl 330 core;

func clip_quad_to_hemisphere(inout vec3 L[5], out int n): float result
<<<
    // source grabbed directly from Eric Heitz work
    // see: https://eheitzresearch.wordpress.com/415-2/ for details

    // detect clipping config
    int config = 0;
    if (L[0].z > 0.0) config += 1;
    if (L[1].z > 0.0) config += 2;
    if (L[2].z > 0.0) config += 4;
    if (L[3].z > 0.0) config += 8;

    // clip
    n = 0;

    if (config == 0)
    {
        // clip all
    }
    else if (config == 1) // V1 clip V2 V3 V4
    {
        n = 3;
        L[1] = -L[1].z * L[0] + L[0].z * L[1];
        L[2] = -L[3].z * L[0] + L[0].z * L[3];
    }
    else if (config == 2) // V2 clip V1 V3 V4
    {
        n = 3;
        L[0] = -L[0].z * L[1] + L[1].z * L[0];
        L[2] = -L[2].z * L[1] + L[1].z * L[2];
    }
    else if (config == 3) // V1 V2 clip V3 V4
    {
        n = 4;
        L[2] = -L[2].z * L[1] + L[1].z * L[2];
        L[3] = -L[3].z * L[0] + L[0].z * L[3];
    }
    else if (config == 4) // V3 clip V1 V2 V4
    {
        n = 3;
        L[0] = -L[3].z * L[2] + L[2].z * L[3];
        L[1] = -L[1].z * L[2] + L[2].z * L[1];
    }
    else if (config == 5) // V1 V3 clip V2 V4) impossible
    {
        n = 0;
    }
    else if (config == 6) // V2 V3 clip V1 V4
    {
        n = 4;
        L[0] = -L[0].z * L[1] + L[1].z * L[0];
        L[3] = -L[3].z * L[2] + L[2].z * L[3];
    }
    else if (config == 7) // V1 V2 V3 clip V4
    {
        n = 5;
        L[4] = -L[3].z * L[0] + L[0].z * L[3];
        L[3] = -L[3].z * L[2] + L[2].z * L[3];
    }
    else if (config == 8) // V4 clip V1 V2 V3
    {
        n = 3;
        L[0] = -L[0].z * L[3] + L[3].z * L[0];
        L[1] = -L[2].z * L[3] + L[3].z * L[2];
        L[2] =  L[3];
    }
    else if (config == 9) // V1 V4 clip V2 V3
    {
        n = 4;
        L[1] = -L[1].z * L[0] + L[0].z * L[1];
        L[2] = -L[2].z * L[3] + L[3].z * L[2];
    }
    else if (config == 10) // V2 V4 clip V1 V3) impossible
    {
        n = 0;
    }
    else if (config == 11) // V1 V2 V4 clip V3
    {
        n = 5;
        L[4] = L[3];
        L[3] = -L[2].z * L[3] + L[3].z * L[2];
        L[2] = -L[2].z * L[1] + L[1].z * L[2];
    }
    else if (config == 12) // V3 V4 clip V1 V2
    {
        n = 4;
        L[1] = -L[1].z * L[2] + L[2].z * L[1];
        L[0] = -L[0].z * L[3] + L[3].z * L[0];
    }
    else if (config == 13) // V1 V3 V4 clip V2
    {
        n = 5;
        L[4] = L[3];
        L[3] = L[2];
        L[2] = -L[1].z * L[2] + L[2].z * L[1];
        L[1] = -L[1].z * L[0] + L[0].z * L[1];
    }
    else if (config == 14) // V2 V3 V4 clip V1
    {
        n = 5;
        L[4] = -L[0].z * L[3] + L[3].z * L[0];
        L[0] = -L[0].z * L[1] + L[1].z * L[0];
    }
    else if (config == 15) // V1 V2 V3 V4
    {
        n = 4;
    }

    if (n == 3)
        L[3] = L[0];
    if (n == 4)
        L[4] = L[0];

    result = 0;
>>>;

func ltc_integrate_edge(vec3 v1, vec3 v2): float result
<<<
    float cosTheta = dot(v1, v2);
    float theta = acos(cosTheta);
    result = cross(v1, v2).z * ((theta > 0.001) ? theta/sin(theta) : 1.0);
>>>;

func ltc_evaluate(
    vec3 normal,
    vec3 view_direction,
    vec3 position,
    mat3 minv,
    vec3 points[4],
    bool two_sided
): vec3 result
<<<
    vec3 T1 = normalize(view_direction - normal * dot(view_direction, normal));
    vec3 T2 = cross(normal, T1);

    minv = minv * transpose(mat3(T1, T2, normal));

    // polygon (allocate 5 vertices for clipping)
    vec3 L[5];
    L[0] = minv * (points[0] - position);
    L[1] = minv * (points[1] - position);
    L[2] = minv * (points[2] - position);
    L[3] = minv * (points[3] - position);

    int n;
    clip_quad_to_hemisphere(L, n);

    if (n == 0)
        return vec3(0, 0, 0);

    // project onto sphere
    L[0] = normalize(L[0]);
    L[1] = normalize(L[1]);
    L[2] = normalize(L[2]);
    L[3] = normalize(L[3]);
    L[4] = normalize(L[4]);

    float sum = 0.0;

    sum += ltc_integrate_edge(L[0], L[1]);
    sum += ltc_integrate_edge(L[1], L[2]);
    sum += ltc_integrate_edge(L[2], L[3]);
    if (n >= 4)
        sum += ltc_integrate_edge(L[3], L[4]);
    if (n == 5)
        sum += ltc_integrate_edge(L[4], L[0]);

    sum = two_sided ? abs(sum) : max(0.0, sum);
    result = vec3(sum, sum, sum);
>>> requires { clip_quad_to_hemisphere, ltc_integrate_edge };
